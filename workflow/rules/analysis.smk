# SPDX-FileCopyrightText: 2026 Koen van Greevenbroek
#
# SPDX-License-Identifier: GPL-3.0-or-later


rule prepare_faostat_emissions:
    input:
        gt_csv="data/downloads/faostat/GT.parquet",
    output:
        "<processing>/{name}/faostat_emissions.csv",
    params:
        year=config["baseline_year"],
    group:
        "prep"
    resources:
        runtime="1m",
        mem_mb=1400,
    log:
        "<logs>/{name}/prepare_faostat_emissions.log",
    benchmark:
        "<benchmarks>/{name}/prepare_faostat_emissions.tsv"
    script:
        "../scripts/prepare_faostat_emissions.py"


_ANALYSIS_SCRIPTS = sorted(
    str(path) for path in Path("workflow/scripts/analysis").glob("extract_*.py")
)

from workflow.scripts.solve_namespace import ANALYSIS_OUTPUT_NAMES

# Per-scenario parquet outputs of analyze_model. The list of names is
# the single source of truth in workflow/scripts/solve_namespace.py;
# only the path template lives here.
_ANALYSIS_OUTPUTS = {
    name: f"<results>/{{name}}/analysis/scen-{{scenario}}/{name}.parquet"
    for name in ANALYSIS_OUTPUT_NAMES
}


if config["solving"]["inline_analysis"]:

    # NOTE: When changing inputs or params on solve_and_analyze_model, also
    # update tools/export-solve-manifest which mirrors these for the HPC manifest.
    rule solve_and_analyze_model:
        """Solve the model and run analysis in one process (no intermediate .nc).

        Equivalent to solve_model + analyze_model but avoids the IO cost of
        writing and re-reading the solved network.  Enabled via
        ``solving.inline_analysis: true``.
        """
        input:
            unpack(solve_model_inputs),
            population="<processing>/{name}/population.csv",
            analysis_scripts=_ANALYSIS_SCRIPTS,
        threads: lambda w: get_solver_threads(get_effective_config(w.scenario))
        params:
            # --- solve params ---
            health_enabled=lambda w: get_effective_config(w.scenario)["health"][
                "enabled"
            ],
            health_risk_factors=lambda w: get_effective_config(w.scenario)["health"][
                "risk_factors"
            ],
            health_risk_cause_map=lambda w: get_effective_config(w.scenario)["health"][
                "risk_cause_map"
            ],
            health_value_per_yll=lambda w: get_effective_config(w.scenario)["health"][
                "value_per_yll"
            ],
            health_segment_formulation=lambda w: get_effective_config(w.scenario)[
                "health"
            ]["segment_formulation"],
            health_relax_and_fix_max_gap=lambda w: get_effective_config(w.scenario)[
                "health"
            ]["relax_and_fix_max_gap"],
            ghg_price=lambda w: get_effective_config(w.scenario)["emissions"][
                "ghg_price"
            ],
            solver=lambda w: get_effective_config(w.scenario)["solving"]["solver"],
            solver_options=lambda w: solver_options_with_overrides(
                get_effective_config(w.scenario)
            ),
            io_api=lambda w: get_effective_config(w.scenario)["solving"]["io_api"],
            calculate_fixed_duals=lambda w: get_effective_config(w.scenario)[
                "solving"
            ]["calculate_fixed_duals"],
            macronutrients=lambda w: get_effective_config(w.scenario)["macronutrients"],
            food_group_constraints=lambda w: get_effective_config(w.scenario)[
                "food_groups"
            ]["constraints"],
            enforce_baseline=lambda w: get_effective_config(w.scenario)["validation"][
                "enforce_baseline_diet"
            ],
            deviation_penalty=lambda w: get_effective_config(w.scenario)[
                "deviation_penalty"
            ],
            reallocation_cap=lambda w: get_effective_config(w.scenario)[
                "reallocation_cap"
            ],
            animal_growth_cap=lambda w: get_effective_config(w.scenario)["validation"][
                "animal_growth_cap"
            ],
            crop_growth_cap=lambda w: get_effective_config(w.scenario)["validation"][
                "crop_growth_cap"
            ],
            food_utility_piecewise=lambda w: get_effective_config(w.scenario)[
                "food_utility_piecewise"
            ],
            fix_within_group_ratios=lambda w: get_effective_config(w.scenario)[
                "food_groups"
            ]["fix_within_group_ratios"],
            sensitivity=lambda w: get_effective_config(w.scenario)["sensitivity"],
            reforestation_cap=lambda w: get_effective_config(w.scenario)["land"][
                "reforestation_cap"
            ],
            production_value=lambda w: get_effective_config(w.scenario)[
                "production_value"
            ],
            food_energy=lambda w: get_effective_config(w.scenario)["food_energy"][
                "floor"
            ],
            biodiversity=lambda w: get_effective_config(w.scenario)["biodiversity"][
                "cap"
            ],
            production_concentration=lambda w: get_effective_config(w.scenario)[
                "production_concentration"
            ]["cap"],
            protein=lambda w: get_effective_config(w.scenario)["protein"]["floor"],
            affordability=lambda w: get_effective_config(w.scenario)["affordability"][
                "cost_cap"
            ],
            emissions_cap=lambda w: get_effective_config(w.scenario)["emissions"]["cap"],
            forage_calibration_enabled=lambda w: get_effective_config(w.scenario)[
                "grazing"
            ]["grassland_forage_calibration"]["enabled"],
            forage_overlap_crops=config["grazing"]["forage_overlap_crops"],
            exogenous_feed_calibration_enabled=lambda w: get_effective_config(
                w.scenario
            )["exogenous_feed_calibration"]["enabled"],
            enforce_baseline_feed=config["validation"]["enforce_baseline_feed"],
            regional_limit=lambda w: get_effective_config(w.scenario)["land"][
                "regional_limit"
            ],
            biofuel_demand_scale=lambda w: get_effective_config(w.scenario)["biomass"][
                "biofuel_demand_scale"
            ],
            ghg_pricing_enabled=lambda w: get_effective_config(w.scenario)[
                "emissions"
            ]["ghg_pricing_enabled"],
            water_scarcity_tiers=config["water"]["supply"]["scarcity_tiers"],
            water_availability=config["water"]["data"]["availability"],
            water_scarcity_pricing_enabled=lambda w: get_effective_config(w.scenario)[
                "water_scarcity"
            ]["pricing_enabled"],
            water_scarcity_price=lambda w: get_effective_config(w.scenario)[
                "water_scarcity"
            ]["price"],
            water_scarcity_cap=lambda w: get_effective_config(w.scenario)[
                "water_scarcity"
            ]["cap_mm3_world_eq"],
            water_scarcity_nonrenewable_cf=lambda w: get_effective_config(w.scenario)[
                "water_scarcity"
            ]["nonrenewable_cf"],
            groundwater_pricing_enabled=lambda w: get_effective_config(w.scenario)[
                "groundwater_depletion"
            ]["pricing_enabled"],
            groundwater_price=lambda w: get_effective_config(w.scenario)[
                "groundwater_depletion"
            ]["price"],
            groundwater_cap=lambda w: get_effective_config(w.scenario)[
                "groundwater_depletion"
            ]["cap_mm3"],
            food_incentives_enabled=lambda w: get_effective_config(w.scenario)[
                "food_incentives"
            ]["enabled"],
            equal_by_country_source=lambda w: get_effective_config(w.scenario)[
                "food_groups"
            ]["equal_by_country_source"],
            slack_marginal_cost=config["validation"]["slack_marginal_cost"],
            residue_max_feed_fraction=config["residues"]["max_feed_fraction"],
            residue_max_feed_fraction_by_region=config["residues"][
                "max_feed_fraction_by_region"
            ],
            countries=config["countries"],
            export_for_tuning=lambda w: get_effective_config(w.scenario)["solving"][
                "export_for_tuning"
            ],
            netcdf=lambda w: get_effective_config(w.scenario)["netcdf"],
            scenario_hash=lambda w: scenario_override_hash(w.scenario),
            # --- analysis params ---
            ch4_gwp=config["emissions"]["ch4_to_co2_factor"],
            n2o_gwp=config["emissions"]["n2o_to_co2_factor"],
        output:
            **_ANALYSIS_OUTPUTS,
        retries: 2
        resources:
            runtime=solve_model_runtime,
            mem_mb=lambda w, attempt: solve_model_mem_mb(w, attempt) + 500,
        log:
            "<logs>/{name}/solve_and_analyze_model_scen-{scenario}.log",
        benchmark:
            "<benchmarks>/{name}/solve_and_analyze_model_scen-{scenario}.tsv"
        script:
            "../scripts/solve_and_analyze_model.py"

else:

    rule analyze_model:
        """Analyze solved network: extract statistics, emissions, GHG attribution, and health impacts.

        Loads the network once and runs all analysis steps in a single pass,
        passing intermediate DataFrames in memory rather than writing/re-reading them.
        """
        input:
            # Health processing inputs only when this scenario enables health;
            # otherwise analyze_model writes empty health outputs.
            unpack(
                lambda w: (
                    health_input_paths(w.name)
                    if get_effective_config(w.scenario)["health"]["enabled"]
                    else {}
                )
            ),
            network="<results>/{name}/solved/model_scen-{scenario}.nc",
            food_groups="data/curated/food_groups.csv",
            m49="data/curated/M49-codes.csv",
            population="<processing>/{name}/population.csv",
            analysis_scripts=_ANALYSIS_SCRIPTS,
        params:
            ghg_price=lambda w: get_effective_config(w.scenario)["emissions"][
                "ghg_price"
            ],
            ch4_gwp=config["emissions"]["ch4_to_co2_factor"],
            n2o_gwp=config["emissions"]["n2o_to_co2_factor"],
            health_enabled=lambda w: get_effective_config(w.scenario)["health"][
                "enabled"
            ],
            health_value_per_yll=lambda w: get_effective_config(w.scenario)["health"][
                "value_per_yll"
            ],
            health_risk_factors=config["health"]["risk_factors"],
        output:
            **_ANALYSIS_OUTPUTS,
        group:
            "analyze_model"
        resources:
            runtime="5m",
            mem_mb=1500,
        log:
            "<logs>/{name}/analyze_model_scen-{scenario}.log",
        benchmark:
            "<benchmarks>/{name}/analyze_model_scen-{scenario}.tsv"
        script:
            "../scripts/analysis/analyze_model.py"


def _sensitivity_generator_group(gen):
    """Extract the group name from a sensitivity generator name pattern.

    E.g. "gsa_{sample_id}" -> "gsa", "gsa-l1-low_{sample_id}" -> "gsa-l1-low".
    """
    return gen["name"].split("_{")[0]


def _sensitivity_generator(wildcards):
    """Return the sensitivity generator whose group matches the wildcard."""
    raw_defs = config["scenarios"]
    group = wildcards.group

    generators = [
        gen
        for gen in raw_defs.get("_generators", [])
        if gen.get("mode") == "sensitivity"
    ]
    if not generators:
        raise ValueError("No sensitivity generator found in scenarios")

    for gen in generators:
        if _sensitivity_generator_group(gen) == group:
            return gen

    available = [_sensitivity_generator_group(gen) for gen in generators]
    raise ValueError(
        f"No sensitivity generator matches group '{group}'. "
        f"Available groups: {available}"
    )


_ANALYSIS_FILES = (
    "objective_breakdown.parquet",
    "net_emissions.parquet",
    "land_use.parquet",
    "health_totals.parquet",
)


def _sensitivity_scenario_names(wildcards):
    """Return all sensitivity scenario names matching the group in expansion order.

    Default behaviour declares every scenario the generator would produce so
    Snakemake can drive the full solve→analyse→surrogate chain in a single
    invocation.  Set ``sensitivity_analysis.discover_scenarios_on_disk: true``
    when the solve+analyse phase runs *outside* Snakemake (e.g. cluster
    sweeps via ``tools/batch-solve``) and the surrogate should be fit over
    whatever scenarios happen to have complete analysis outputs on disk.
    See :func:`_filter_existing_scenarios` for the guardrails.
    """
    import re

    generator = _sensitivity_generator(wildcards)
    pattern = re.escape(generator["name"]).replace(r"\{sample_id\}", r"\d+") + "$"

    all_scenarios = list_scenarios()
    matching = [s for s in all_scenarios if re.match(pattern, s)]
    if not matching:
        raise ValueError(
            f"No scenarios found matching pattern '{pattern}'. "
            f"Available scenarios: {all_scenarios}"
        )

    if config["sensitivity_analysis"]["discover_scenarios_on_disk"]:
        return _filter_existing_scenarios(wildcards, matching, generator["name"])
    return matching


def _filter_existing_scenarios(wildcards, scenarios, group_label):
    """Drop scenarios whose analysis outputs are not all present on disk.

    Used only when ``sensitivity_analysis.discover_scenarios_on_disk`` is true.
    Errors out if more than half the scenarios are missing — that almost
    always means the solve+analyse phase has not been run yet, and silently
    fitting a surrogate on the remainder would produce a misleading model.
    """
    from pathlib import Path

    analysis_root = Path(PATH_ROOTS["results"]) / wildcards.name / "analysis"
    available = [
        s
        for s in scenarios
        if all((analysis_root / f"scen-{s}" / f).is_file() for f in _ANALYSIS_FILES)
    ]
    n_total = len(scenarios)
    n_missing = n_total - len(available)
    if n_missing == 0:
        return available
    if n_missing > n_total // 2:
        raise ValueError(
            f"sensitivity_analysis.discover_scenarios_on_disk is true but "
            f"{n_missing} of {n_total} scenarios for group '{group_label}' "
            f"have incomplete analysis outputs (>50% missing). Run the "
            f"solve+analyse phase before fitting the surrogate."
        )
    print(
        f"[sensitivity] group={group_label}: dropping {n_missing} of {n_total} "
        f"scenarios with incomplete analysis outputs (discover_scenarios_on_disk=true)."
    )
    return available


def _sensitivity_scenario_inputs(wildcards):
    """Generate input files for all sensitivity scenarios.

    Mirrors :func:`_sensitivity_scenario_names`: when
    ``sensitivity_analysis.discover_scenarios_on_disk`` is false (default),
    every expected scenario is declared so Snakemake drives the upstream
    solves; when true, only scenarios with complete outputs on disk are
    declared.
    """
    all_scenarios = _sensitivity_scenario_names(wildcards)
    return [
        f"<results>/{wildcards.name}/analysis/scen-{s}/{f}"
        for s in all_scenarios
        for f in _ANALYSIS_FILES
    ]


def _sensitivity_method_config(wildcards):
    """Return the method-specific config dict from sensitivity_analysis.methods."""
    method = wildcards.method
    methods = config["sensitivity_analysis"]["methods"]
    if method not in methods:
        raise ValueError(
            f"Unknown sensitivity method '{method}'. "
            f"Available methods: {list(methods.keys())}"
        )
    return dict(methods[method])


def _sensitivity_sobol_config(wildcards):
    """Return the ``sensitivity_analysis.sobol`` config block."""
    return dict(config["sensitivity_analysis"]["sobol"])


def _sensitivity_slice_grid(wildcards):
    """Build a conditioning grid for slice parameters.

    Returns a dict mapping each slice parameter name to a list of grid
    values between its min and max.  Log-uniform parameters get
    log-spaced grids; all others get linearly-spaced grids.  Grid
    resolution is read from ``sensitivity_analysis.sobol.grid_resolution``.
    """
    import numpy as _np

    from scenario_generators import build_chaospy_distribution

    generator = _sensitivity_generator(wildcards)
    n_grid = int(config["sensitivity_analysis"]["sobol"]["grid_resolution"])
    slice_params = generator.get("slice_parameters", [])
    grid = {}
    for sp in slice_params:
        spec = generator["parameters"][sp]
        dist = build_chaospy_distribution(spec)
        lo, hi = float(dist.lower[0]), float(dist.upper[0])
        if spec.get("distribution") == "log_uniform":
            values = _np.geomspace(lo, hi, n_grid)
        else:
            values = _np.linspace(lo, hi, n_grid)
        grid[sp] = [float(v) for v in values]
    return grid


rule build_surrogate:
    """Fit a surrogate model over the GSA Sobol design and persist it.

    Produces a :class:`SurrogateBundle` pickle plus a flat validation
    parquet (surrogate fit quality per output target).  Downstream rules
    (Sobol computation, uncertainty-band plots) load the pickle and do
    not refit.

    The {group} wildcard identifies the scenario sampling group (e.g.,
    "gsa"); {method} selects the surrogate type (one of the keys under
    ``sensitivity_analysis.methods``).
    """
    input:
        _sensitivity_scenario_inputs,
    params:
        scenario_names=_sensitivity_scenario_names,
        generator_spec=lambda w: _sensitivity_generator(w),
        method_config=_sensitivity_method_config,
        holdout_fraction=lambda w: config["sensitivity_analysis"]["holdout_fraction"],
        outputs_spec=lambda w: config["sensitivity_analysis"]["outputs"],
    output:
        surrogate="<results>/{name}/surrogates/surrogate_{group}_{method}.pkl",
        validation="<results>/{name}/surrogates/surrogate_validation_{group}_{method}.parquet",
    threads: lambda w: config["sensitivity_analysis"]["threads"]
    group:
        "analysis_plot"
    resources:
        runtime="10m",
        mem_mb=4000,
    log:
        "<logs>/{name}/build_surrogate_{group}_{method}.log",
    benchmark:
        "<benchmarks>/{name}/build_surrogate_{group}_{method}.tsv"
    script:
        "../scripts/analysis/build_surrogate.py"


rule compute_sobol_sensitivity:
    """Compute global and conditional Sobol indices from a surrogate bundle.

    Loads the pickle produced by ``build_surrogate`` and evaluates Sobol
    indices: analytically for PCE, Saltelli pick-freeze MC for the
    regressor-based surrogates (RF, XGB, MLP).

    The {group} wildcard identifies the scenario sampling group (e.g.,
    "gsa"); {method} selects which surrogate to use.
    """
    input:
        surrogate="<results>/{name}/surrogates/surrogate_{group}_{method}.pkl",
    params:
        sobol_config=_sensitivity_sobol_config,
        slice_grid=_sensitivity_slice_grid,
        outputs_spec=lambda w: config["sensitivity_analysis"]["outputs"],
    output:
        global_indices="<results>/{name}/analysis/sobol_global_indices_{group}_{method}.parquet",
        conditional_indices="<results>/{name}/analysis/sobol_conditional_indices_{group}_{method}.parquet",
        conditional_joint_indices="<results>/{name}/analysis/sobol_conditional_joint_indices_{group}_{method}.parquet",
    threads: lambda w: config["sensitivity_analysis"]["threads"]
    group:
        "analysis_plot"
    resources:
        runtime="5m",
        mem_mb=2000,
    log:
        "<logs>/{name}/compute_sobol_sensitivity_{group}_{method}.log",
    benchmark:
        "<benchmarks>/{name}/compute_sobol_sensitivity_{group}_{method}.tsv"
    script:
        "../scripts/analysis/compute_sobol_sensitivity.py"
